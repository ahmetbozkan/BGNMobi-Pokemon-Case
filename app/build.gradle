plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
}

android {
    compileSdk buildConfig.compileSdkVersion

    defaultConfig {
        applicationId buildConfig.applicationId
        minSdk buildConfig.minSdkVersion
        targetSdk buildConfig.targetSdkVersion
        versionCode generateVersionCode()
        versionName generateVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "BASE_URL", base_url)
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
    }
}

dependencies {
    // navigation component
    implementation deps.androidx.navigation

    // dagger - hilt
    implementation deps.dagger.hilt
    kapt deps.dagger.kapt

    // lifecycle
    implementation deps.androidx.lifecycle.fragment
    implementation deps.androidx.lifecycle.viewmodel
    implementation deps.androidx.lifecycle.livedata
    implementation deps.androidx.lifecycle.activity

    // coroutines
    implementation deps.kotlin.coroutines

    // datastore
    implementation deps.datastore

    // glide
    implementation deps.glide

    // gson
    implementation deps.gson

    // swiperefresh
    implementation deps.androidx.swipe_refresh

    // network (retrofit - okhttp)
    implementation deps.network

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

private Integer generateVersionCode() {
    return buildConfig.version.major * 10000 + buildConfig.version.minor * 100 + buildConfig.version.patch
}

private String generateVersionName() {
    return "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
}